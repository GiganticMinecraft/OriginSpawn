plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.23'
    id 'com.github.jmongard.git-semver-plugin' version '0.12.6'
}

semver {
    releaseTagNameFormat = "v%s"
}

group = 'click.seichi'
version = semver.version

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    implementation 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    embed 'org.jetbrains.kotlin:kotlin-stdlib'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}
